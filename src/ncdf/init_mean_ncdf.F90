!$Id: init_mean_ncdf.F90,v 1.1 2004-03-29 15:38:10 kbk Exp $
#include "cppdefs.h"
!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: init_mean_ncdf -
!
! !INTERFACE:
   subroutine init_mean_ncdf(fn,title,starttime)
!
! !DESCRIPTION:
!
! !USES:
   use ncdf_mean
   use domain, only: ioff,joff,imin,imax,jmin,jmax
   use domain, only: iimin,iimax,jjmin,jjmax,kmax
#if defined(SPHERICAL)
   use domain, only: lonc,latc
#endif
#if ! defined(SPHERICAL)
   use domain, only: xc,yc
#endif
   use domain, only: grid_type,vert_cord
   IMPLICIT NONE
!
! !INPUT PARAMETERS:
   character(len=*), intent(in)        :: fn,title,starttime
!
! !INPUT/OUTPUT PARAMETERS:
!
! !OUTPUT PARAMETERS:
!
! !REVISION HISTORY:
!  Original author(s): Adolf Stips & Karsten Bolding
!
!  $Log: init_mean_ncdf.F90,v $
!  Revision 1.1  2004-03-29 15:38:10  kbk
!  possible to store calculated mean fields
! 
!
! !LOCAL VARIABLES:
   integer                   :: i,j,k,err
   character(32)             :: xname,yname,zname,xunits,yunits,zunits
   character(32)             :: gridname,vertname
   integer                   :: scalar(1),axisdim(1),f3_dims(3),f4_dims(4)
   REALTYPE                  :: fv,mv,vr(2)
   REALTYPE                  :: x
   character(len=80)         :: history,tts
!EOP
!-------------------------------------------------------------------------
!BOC
   include "netcdf.inc"

   xlen = iimax-iimin+1
   ylen = jjmax-jjmin+1
   zlen = kmax+1

   select case (grid_type)
      case (1)
         xname = 'xax'
         yname = 'yax'
         xunits = 'meters'
         yunits = 'meters'
         gridname='cartesian'
      case (2)
         xname = 'lon'
         yname = 'lat'
         xunits = 'degrees_east'
         yunits = 'degrees_north'
         gridname='spherical'
      case (3)
         xname = 'xdim'
         yname = 'ydim'
         xunits = 'curvi-x'
         yunits = 'curvi-y'
         gridname='curvi-linear'
      case default
   end select

   select case (vert_cord)
      case (1)
         zname = 'zax'
         zunits = 'sigma_level'
         vertname='sigma coordinates'
      case (2)
         zname = 'zax'
         zunits = 'meters'
         vertname='z-levels'
      case (3)
         zname = 'zax'
         zunits = 'level'
         vertname='general vertical coordinates'
      case default
   end select
   err = nf_create(fn, NF_CLOBBER, ncid)
   if (err .NE. NF_NOERR) go to 10

!  dimensions
   err = nf_def_dim(ncid,xname,xlen,x_dim)
   if (err .NE. NF_NOERR) go to 10

   err = nf_def_dim(ncid,yname,ylen,y_dim)
   if (err .NE. NF_NOERR) go to 10

   err = nf_def_dim(ncid,zname,zlen,z_dim)
   if (err .NE. NF_NOERR) go to 10

   err = nf_def_dim(ncid,'time',NF_UNLIMITED,time_dim)
   if (err .NE. NF_NOERR) go to 10

   f3_dims(3)= time_dim
   f3_dims(2)= y_dim
   f3_dims(1)= x_dim

   f4_dims(4)= time_dim
   f4_dims(3)= z_dim
   f4_dims(2)= y_dim
   f4_dims(1)= x_dim

   history = 'Generated by getm, ver. '//RELEASE
   tts = 'seconds since '//starttime

!  info on offset, grid type and vertical coordinates
   scalar(1) = 0
   err = nf_def_var(ncid,'grid_type',NF_INT,0,scalar,grid_type_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,grid_type_id,units=gridname)

   err = nf_def_var(ncid,'vert_cord',NF_INT,0,scalar,vert_cord_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,vert_cord_id,units=vertname)

   err = nf_def_var(ncid,'ioff',NF_INT,0,scalar,ioff_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,ioff_id,long_name='index offset (i)')

   err = nf_def_var(ncid,'joff',NF_INT,0,scalar,joff_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,joff_id,long_name='index offset (j)')

!  time
   axisdim(1) = time_dim
   err = nf_def_var(ncid,'time',NF_REAL,1,axisdim,time_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,time_id,units=trim(tts),long_name='time')

!  coordinate variables
   select case (grid_type)
      case (1)
#if ! ( defined(SPHERICAL) || defined(CURVILINEAR) )
         axisdim(1) = x_dim
         err = nf_def_var(ncid,xname,NF_REAL,1,axisdim,xc_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,xc_id,units=xunits)
         axisdim(1) = y_dim
         err = nf_def_var(ncid,yname,NF_REAL,1,axisdim,yc_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,yc_id,units=yunits)
#endif
      case (2)
#if defined(SPHERICAL)
         axisdim(1) = x_dim
         err = nf_def_var(ncid,xname,NF_REAL,1,axisdim,lonc_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,lonc_id,units=xunits)
         axisdim(1) = y_dim
         err = nf_def_var(ncid,yname,NF_REAL,1,axisdim,latc_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,latc_id,units=yunits)
#endif
      case (3)
#if defined(CURVILINEAR)
!  do something about curvi-linear coordinates - define xu,xv,....
#endif
      case default
   end select

   axisdim(1) = z_dim
   err = nf_def_var(ncid,zname,NF_REAL,1,axisdim,z_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,z_id,units=zunits)

!  bathymetry
   err = nf_def_var(ncid,'bathymetry',NF_REAL,2,f3_dims,bathymetry_id)
   if (err .NE. NF_NOERR) go to 10
   fv = h_missing
   mv = h_missing
   vr(1) = -5.
   vr(2) = 4000.
   call set_attributes(ncid,bathymetry_id,  &
          long_name='bathymetry',units='meters',          &
          FillValue=fv,missing_value=mv,valid_range=vr)

!  now to the variables
!  short wave radiation 
   fv = swr_missing; mv = swr_missing; vr(1) = 0; vr(2) = 1500.
   err = nf_def_var(ncid,'swrmean',NF_REAL,3,f3_dims,swrmean_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,swrmean_id,  &
          long_name='mean short wave radiation',units='W/m2', &
          FillValue=fv,missing_value=mv,valid_range=vr)

! Ustar at bottom

   fv = vel_missing; mv = vel_missing; vr(1) = -1; vr(2) = 1.
   err = nf_def_var(ncid,'ustarmean',NF_REAL,3,f3_dims,ustarmean_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,ustarmean_id,  &
          long_name='bottom friction velocity',units='m/s', &
          FillValue=fv,missing_value=mv,valid_range=vr)

! Standard deviation of ustar

   fv = vel_missing; mv = vel_missing; vr(1) = 0; vr(2) = 1.
   err = nf_def_var(ncid,'ustar2mean',NF_REAL,3,f3_dims,ustar2mean_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,ustar2mean_id,  &
          long_name='stdev of bottom friction velocity',units='m/s', &
          FillValue=fv,missing_value=mv,valid_range=vr)

   select case (vert_cord)
      case (1)
      case (2)
         STDERR 'store z-levels'
         stop 'init_mean_ncdf'
      case (3)
         fv = h_missing
         mv = h_missing
         err = nf_def_var(ncid,'hmean',NF_REAL,4,f4_dims,hmean_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,hmean_id, &
                             long_name='mean layer thickness',  &
                             units='meters',FillValue=fv,missing_value=mv)
      case default
   end select

   fv = vel_missing
   mv = vel_missing
   vr(1) = -3.
   vr(2) =  3.

!  zonal velocity
   err = nf_def_var(ncid,'uumean',NF_REAL,4,f4_dims,uumean_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,uumean_id, &
          long_name='mean zonal vel.',units='m/s', &
          FillValue=fv,missing_value=mv,valid_range=vr)

!  meridional velocity
   err = nf_def_var(ncid,'vvmean',NF_REAL,4,f4_dims,vvmean_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,vvmean_id, &
          long_name='mean meridional vel.',units='m/s', &
          FillValue=fv,missing_value=mv,valid_range=vr)

!  vertical velocity
   err = nf_def_var(ncid,'wmean',NF_REAL,4,f4_dims,wmean_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,wmean_id, &
          long_name='mean vertical vel.',units='m/s', &
          FillValue=fv,missing_value=mv,valid_range=vr)

   fv = salt_missing
   mv = salt_missing
   vr(1) =  0.
   vr(2) = 40.
   err = nf_def_var(ncid,'saltmean',NF_REAL,4,f4_dims,saltmean_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,saltmean_id, &
          long_name='mean salinity',units='PSU', &
          FillValue=fv,missing_value=mv,valid_range=vr)

   fv = temp_missing
   mv = temp_missing
   vr(1) =  0.
   vr(2) = 40.
   err = nf_def_var(ncid,'tempmean',NF_REAL,4,f4_dims,tempmean_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,tempmean_id, &
          long_name='mean temperature',units='degC',&
          FillValue=fv,missing_value=mv,valid_range=vr)

!  globals
   err = nf_put_att_text(ncid,NF_GLOBAL,'title',LEN_TRIM(title),title)
   if (err .NE. NF_NOERR) go to 10

   err = nf_put_att_text(ncid,NF_GLOBAL,'history',LEN_TRIM(history),history)
   if (err .NE. NF_NOERR) go to 10

   ! leave define mode
   err = nf_enddef(ncid)
   if (err .NE. NF_NOERR) go to 10

   return

   10 FATAL 'init_mean_ncdf: ',nf_strerror(err)
   stop 'init_mean_ncdf'
   end subroutine init_mean_ncdf
!EOC

!-----------------------------------------------------------------------
! Copyright (C) 2004 - Adolf Stips and Karsten Bolding (BBH)           !
!-----------------------------------------------------------------------
