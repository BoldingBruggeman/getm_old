!$Id: init_mean_ncdf.F90,v 1.2 2005-04-25 09:32:34 kbk Exp $
#include "cppdefs.h"
!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: Initialise mean netCDf variables
!
! !INTERFACE:
   subroutine init_mean_ncdf(fn,title,starttime)
!
! !DESCRIPTION:
!
! !USES:
   use netcdf
   use exceptions
   use ncdf_common
   use ncdf_mean
   use domain, only: ioff,joff
   use domain, only: imin,imax,jmin,jmax,kmax
   use domain, only: vert_cord

   IMPLICIT NONE
!
! !INPUT PARAMETERS:
   character(len=*), intent(in)        :: fn,title,starttime
!
! !DEFINED PARAMETERS:
   logical,    parameter               :: init3d=.true.
!
! !REVISION HISTORY:
!  Original author(s): Adolf Stips & Karsten Bolding
!
!  $Log: init_mean_ncdf.F90,v $
!  Revision 1.2  2005-04-25 09:32:34  kbk
!  added NetCDF IO rewrite + de-stag of velocities - Umlauf
!
!  Revision 1.1  2004/03/29 15:38:10  kbk
!  possible to store calculated mean fields
! 
!
! !LOCAL VARIABLES:
   integer                   :: err
   integer                   :: scalar(1),f3_dims(3),f4_dims(4)
   REALTYPE                  :: fv,mv,vr(2)
   character(len=80)         :: history,tts
!EOP
!-------------------------------------------------------------------------
!BOC
!  create netCDF file
   err = nf90_create(fn, NF90_CLOBBER, ncid)
   if (err .NE. NF90_NOERR) go to 10

!  initialize all time-independent, grid related variables
   call init_grid_ncdf(ncid,init3d,x_dim,y_dim,z_dim)

!  define unlimited dimension
   err = nf90_def_dim(ncid,'time',NF90_UNLIMITED,time_dim)
   if (err .NE. NF90_NOERR) go to 10

!  netCDF dimension vectors
   f3_dims(3)= time_dim
   f3_dims(2)= y_dim
   f3_dims(1)= x_dim

   f4_dims(4)= time_dim
   f4_dims(3)= z_dim
   f4_dims(2)= y_dim
   f4_dims(1)= x_dim


!  globall settings
   history = 'Generated by getm, ver. '//RELEASE
   tts = 'seconds since '//starttime

!  time
   err = nf90_def_var(ncid,'time',NF90_REAL,time_dim,time_id)
   if (err .NE. NF90_NOERR) go to 10
   call set_attributes(ncid,time_id,units=trim(tts),long_name='time')


!  short wave radiation 
   fv = swr_missing; mv = swr_missing; vr(1) = 0; vr(2) = 1500.
   err = nf90_def_var(ncid,'swrmean',NF90_REAL,f3_dims,swrmean_id)
   if (err .NE. NF90_NOERR) go to 10
   call set_attributes(ncid,swrmean_id,  &
          long_name='mean short wave radiation',units='W/m2', &
          FillValue=fv,missing_value=mv,valid_range=vr)

! Ustar at bottom
   fv = vel_missing; mv = vel_missing; vr(1) = -1; vr(2) = 1.
   err = nf90_def_var(ncid,'ustarmean',NF90_REAL,f3_dims,ustarmean_id)
   if (err .NE. NF90_NOERR) go to 10
   call set_attributes(ncid,ustarmean_id,  &
          long_name='bottom friction velocity',units='m/s', &
          FillValue=fv,missing_value=mv,valid_range=vr)

! Standard deviation of ustar
   fv = vel_missing; mv = vel_missing; vr(1) = 0; vr(2) = 1.
   err = nf90_def_var(ncid,'ustar2mean',NF90_REAL,f3_dims,ustar2mean_id)
   if (err .NE. NF90_NOERR) go to 10
   call set_attributes(ncid,ustar2mean_id,  &
          long_name='stdev of bottom friction velocity',units='m/s', &
          FillValue=fv,missing_value=mv,valid_range=vr)

   select case (vert_cord)
      case (1)
      case (2)    
         call getm_error("init_3d_ncdf()","saving of z-levels disabled")
      case (3)
         fv = hh_missing
         mv = hh_missing
         err = nf90_def_var(ncid,'hmean',NF90_REAL,f4_dims,hmean_id)
         if (err .NE. NF90_NOERR) go to 10
         call set_attributes(ncid,hmean_id, &
                             long_name='mean layer thickness',  &
                             units='meters',FillValue=fv,missing_value=mv)
      case default
   end select

   fv = vel_missing
   mv = vel_missing
   vr(1) = -3.
   vr(2) =  3.

!  zonal velocity
   err = nf90_def_var(ncid,'uumean',NF90_REAL,f4_dims,uumean_id)
   if (err .NE. NF90_NOERR) go to 10
   call set_attributes(ncid,uumean_id, &
          long_name='mean zonal vel.',units='m/s', &
          FillValue=fv,missing_value=mv,valid_range=vr)

!  meridional velocity
   err = nf90_def_var(ncid,'vvmean',NF90_REAL,f4_dims,vvmean_id)
   if (err .NE. NF90_NOERR) go to 10
   call set_attributes(ncid,vvmean_id, &
          long_name='mean meridional vel.',units='m/s', &
          FillValue=fv,missing_value=mv,valid_range=vr)

!  vertical velocity
   err = nf90_def_var(ncid,'wmean',NF90_REAL,f4_dims,wmean_id)
   if (err .NE. NF90_NOERR) go to 10
   call set_attributes(ncid,wmean_id, &
          long_name='mean vertical vel.',units='m/s', &
          FillValue=fv,missing_value=mv,valid_range=vr)

   fv = salt_missing
   mv = salt_missing
   vr(1) =  0.
   vr(2) = 40.
   err = nf90_def_var(ncid,'saltmean',NF90_REAL,f4_dims,saltmean_id)
   if (err .NE. NF90_NOERR) go to 10
   call set_attributes(ncid,saltmean_id, &
          long_name='mean salinity',units='PSU', &
          FillValue=fv,missing_value=mv,valid_range=vr)

   fv = temp_missing
   mv = temp_missing
   vr(1) =  0.
   vr(2) = 40.
   err = nf90_def_var(ncid,'tempmean',NF90_REAL,f4_dims,tempmean_id)
   if (err .NE. NF90_NOERR) go to 10
   call set_attributes(ncid,tempmean_id, &
          long_name='mean temperature',units='degC',&
          FillValue=fv,missing_value=mv,valid_range=vr)

   if (save_mix_analysis) then
      fv = nummix_missing
      mv = nummix_missing
      vr(1) = -100.0
      vr(2) =  100.0

      err = nf90_def_var(ncid,'nummix3d_S',NF90_REAL,f4_dims,nm3dS_id)
      if (err .NE. NF90_NOERR) go to 10
      call set_attributes(ncid,nm3dS_id, &
          long_name='mean numerical mixing of salinity', &
          units='psu**2/s',&
          FillValue=fv,missing_value=mv,valid_range=vr)
      err = nf90_def_var(ncid,'nummix3d_T',NF90_REAL,f4_dims,nm3dT_id)
      if (err .NE. NF90_NOERR) go to 10
      call set_attributes(ncid,nm3dT_id, &
          long_name='mean numerical mixing of temperature', &
          units='degC**2/s',&
          FillValue=fv,missing_value=mv,valid_range=vr)
      err = nf90_def_var(ncid,'phymix3d_S',NF90_REAL,f4_dims,pm3dS_id)
      if (err .NE. NF90_NOERR) go to 10
      call set_attributes(ncid,pm3dS_id, &
          long_name='mean physical mixing of salinity', &
          units='psu**2/s',&
          FillValue=fv,missing_value=mv,valid_range=vr)
      err = nf90_def_var(ncid,'phymix3d_T',NF90_REAL,f4_dims,pm3dT_id)
      if (err .NE. NF90_NOERR) go to 10
      call set_attributes(ncid,pm3dT_id, &
         long_name='mean physical mixing of temperature', &
         units='degC**2/s',&
         FillValue=fv,missing_value=mv,valid_range=vr)

      err = nf90_def_var(ncid,'nummix2d_S',NF90_REAL,f3_dims,nm2dS_id)
      if (err .NE. NF90_NOERR) go to 10
      call set_attributes(ncid,nm2dS_id, &
          long_name='mean, vert.integrated numerical mixing of salinity', &
          units='psu**2 m/s',&
          FillValue=fv,missing_value=mv,valid_range=vr)
      err = nf90_def_var(ncid,'nummix2d_T',NF90_REAL,f3_dims,nm2dT_id)
      if (err .NE. NF90_NOERR) go to 10
      call set_attributes(ncid,nm2dT_id, &
          long_name='mean, vert.integrated numerical mixing of temperature', &
          units='degC**2 m/s',&
          FillValue=fv,missing_value=mv,valid_range=vr)
      err = nf90_def_var(ncid,'phymix2d_S',NF90_REAL,f3_dims,pm2dS_id)
      if (err .NE. NF90_NOERR) go to 10
      call set_attributes(ncid,pm2dS_id, &
          long_name='mean, vert.integrated physical mixing of salinity', &
          units='psu**2 m/s',&
          FillValue=fv,missing_value=mv,valid_range=vr)
      err = nf90_def_var(ncid,'phymix2d_T',NF90_REAL,f3_dims,pm2dT_id)
      if (err .NE. NF90_NOERR) go to 10
      call set_attributes(ncid,pm2dT_id, &
         long_name='mean, vert.integrated physical mixing of temperature', &
         units='degC**2 m/s',&
         FillValue=fv,missing_value=mv,valid_range=vr)
   end if

!  globals
   err = nf90_put_att(ncid,NF90_GLOBAL,'title',trim(title))
   if (err .NE. NF90_NOERR) go to 10

   err = nf90_put_att(ncid,NF90_GLOBAL,'history',trim(history))
   if (err .NE. NF90_NOERR) go to 10

   ! leave define mode
   err = nf90_enddef(ncid)
   if (err .NE. NF90_NOERR) go to 10

   return

   10 FATAL 'init_mean_ncdf: ',nf90_strerror(err)
   stop 'init_mean_ncdf'
   end subroutine init_mean_ncdf
!EOC

!-----------------------------------------------------------------------
! Copyright (C) 2004 - Adolf Stips and Karsten Bolding (BBH)           !
!-----------------------------------------------------------------------
