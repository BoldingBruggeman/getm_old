!$Id: init_3d_ncdf.F90,v 1.14 2009-01-05 09:57:06 kb Exp $
#include "cppdefs.h"
!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: Initialise 3D netCDF variables
!
! !INTERFACE:
   subroutine init_3d_ncdf(fn,title,starttime)
!
! !DESCRIPTION:
!
! !USES:
   use exceptions
   use ncdf_common
   use ncdf_3d
   use domain, only: ioff,joff
   use domain, only: imin,imax,jmin,jmax,kmax
   use domain, only: vert_cord
#ifdef SPM
   use suspended_matter, only: spm_save
#endif
#ifdef GETM_BIO
   use bio_var, only: numc,var_names,var_units,var_long
#endif

   IMPLICIT NONE
!
! !INPUT PARAMETERS:
   character(len=*), intent(in)        :: fn,title,starttime
!
! !DEFINED PARAMETERS:
   logical,    parameter               :: init3d=.true.
!
! !REVISION HISTORY:
!
!  $Log: init_3d_ncdf.F90,v $
!  Revision 1.14  2009-01-05 09:57:06  kb
!  option for storing SS and NN
!
!  Revision 1.13  2007-03-30 13:11:00  hb
!  Use of adaptive and hybrid vertical coordinates technically enabled
!
!  Revision 1.12  2007-02-20 13:52:15  kbk
!  solar radiation -> 3d field - possible to save
!
!  Revision 1.11  2006-03-17 11:06:33  kbk
!  cleaner inclusion of SPM module
!
!  Revision 1.10  2005/09/23 11:27:10  kbk
!  support for biology via GOTMs biology modules
!
!  Revision 1.9  2005/04/25 09:32:34  kbk
!  added NetCDF IO rewrite + de-stag of velocities - Umlauf
!
!  Revision 1.8  2004/10/07 15:46:56  kbk
!  removed wrongly placed  comments for save_nuh
!
!  Revision 1.7  2004/06/15 08:25:57  kbk
!  added supoort for spm - Ruiz
!
!  Revision 1.6  2004/05/04 09:23:51  kbk
!  hydrostatic consistency criteria stored in .3d.nc file
!
!  Revision 1.5  2003/12/16 12:51:04  kbk
!  preparing for proper support for SPM (manuel)
!
!  Revision 1.4  2003/05/09 11:38:26  kbk
!  added proper undef support - based on Adolf Stips patch
!
!  Revision 1.3  2003/04/23 11:53:24  kbk
!  save lat/lon info for spherical grid
!
!  Revision 1.2  2003/04/07 12:51:26  kbk
!  CURVILINEAR --> defined(SPHERICAL) || defined(CURVILINEAR)
!
!  Revision 1.1.1.1  2002/05/02 14:01:46  gotm
!  recovering after CVS crash
!
!  Revision 1.7  2001/10/25 16:16:20  bbh
!  No actual storing of data in init_3d_ncdf.F90 -> save_3d_ncdf.F90
!
!  Revision 1.6  2001/10/23 14:19:20  bbh
!  Stores h if general vertical coordinates
!
!  Revision 1.5  2001/10/23 07:37:17  bbh
!  Saving spm - if calc_spm and save_spm are both true
!
!  Revision 1.4  2001/10/22 08:03:13  bbh
!  Misplaced #else
!
!  Revision 1.3  2001/09/24 14:13:25  bbh
!  xc and yc have changing shape depending on grid_type
!
!  Revision 1.2  2001/09/19 11:20:32  bbh
!  Explicit de-allocates memory when -DFORTRAN90
!
!  Revision 1.1  2001/09/13 14:50:02  bbh
!  Cleaner and smaller NetCDF implementation + better axis support
!
! !LOCAL VARIABLES:
   integer                   :: err
   integer                   :: n,rc
   integer                   :: xlen,ylen,zlen
   integer                   :: scalar(1),axisdim(1),f3_dims(3),f4_dims(4)
   REALTYPE                  :: fv,mv,vr(2)
   character(len=80)         :: history,ts
!EOP
!-------------------------------------------------------------------------
!BOC
   include "netcdf.inc"

!  create netCDF file
   err = nf_create(fn, NF_CLOBBER, ncid)
   if (err .NE. NF_NOERR) go to 10

!  initialize all time-independent, grid related variables
   call init_grid_ncdf(ncid,init3d,x_dim,y_dim,z_dim)

!  length of netCDF dimensions
   xlen = imax-imin+1
   ylen = jmax-jmin+1
   zlen = kmax+1

!  allocate workspace
   allocate(ws(xlen*ylen*zlen),stat=err)
   if (err .ne. 0) call getm_error("init_3d_ncdf()",               &
                                   "error allocating ws")

!  define unlimited dimension
   err = nf_def_dim(ncid,'time',NF_UNLIMITED,time_dim)
   if (err .NE. NF_NOERR) go to 10

!  netCDF dimension vectors
   f3_dims(3)= time_dim
   f3_dims(2)= y_dim
   f3_dims(1)= x_dim

   f4_dims(4)= time_dim
   f4_dims(3)= z_dim
   f4_dims(2)= y_dim
   f4_dims(1)= x_dim


!  gobal settings
   history = 'Generated by getm, ver. '//RELEASE
   ts = 'seconds since '//starttime

!  time
   axisdim(1) = time_dim
   err = nf_def_var(ncid,'time',NF_REAL,1,axisdim,time_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,time_id,units=trim(ts),long_name='time')


!  elevation
   err = nf_def_var(ncid,'elev',NF_REAL,3,f3_dims,elev_id)
   if (err .NE. NF_NOERR) go to 10
   fv = elev_missing
   mv = elev_missing
   vr(1) = -15.
   vr(2) =  15.
   call set_attributes(ncid,elev_id,long_name='elevation',units='m', &
                       FillValue=fv,missing_value=mv,valid_range=vr)

!  depth integrated zonal velocity
   err = nf_def_var(ncid,'u',NF_REAL,3,f3_dims,u_id)
   if (err .NE. NF_NOERR) go to 10
   fv = vel_missing
   mv = vel_missing
   vr(1) = -3.
   vr(2) =  3.
   call set_attributes(ncid,u_id,long_name='int. zonal vel.',units='m/s', &
                       FillValue=fv,missing_value=mv,valid_range=vr)

!  depth integrated meridional velocity
   err = nf_def_var(ncid,'v',NF_REAL,3,f3_dims,v_id)
   if (err .NE. NF_NOERR) go to 10
   fv = vel_missing
   mv = vel_missing
   vr(1) = -3.
   vr(2) =  3.
   call set_attributes(ncid,v_id,long_name='int. meridional vel.',units='m/s', &
                       FillValue=fv,missing_value=mv,valid_range=vr)

   select case (vert_cord)
      case (1)
      case (2)
         call getm_error("init_3d_ncdf()","saving of z-levels disabled")
      case (3,4,5)
         fv = hh_missing
         mv = hh_missing
         err = nf_def_var(ncid,'h',NF_REAL,4,f4_dims,h_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,h_id,long_name='layer thickness',  &
                             units='m',FillValue=fv,missing_value=mv)
      case default
   end select


!  hydrostatic consistency criterion
   err = nf_def_var(ncid,'hcc',NF_REAL,3,f4_dims,hcc_id)
   if (err .NE. NF_NOERR) go to 10
   fv = -_ONE_ 
   mv = -_ONE_
   vr(1) = 0.
   vr(2) = 1.
   call set_attributes(ncid,hcc_id,  &
                       long_name='hcc',units=' ',          &
                       FillValue=fv,missing_value=mv,valid_range=vr)




   if (save_vel) then
      fv = vel_missing
      mv = vel_missing
      vr(1) = -3.
      vr(2) =  3.

!     zonal velocity
      err = nf_def_var(ncid,'uu',NF_REAL,4,f4_dims,uu_id)
      if (err .NE. NF_NOERR) go to 10
      call set_attributes(ncid,uu_id,long_name='zonal vel.',units='m/s', &
                          FillValue=fv,missing_value=mv,valid_range=vr)

!     meridional velocity
      err = nf_def_var(ncid,'vv',NF_REAL,4,f4_dims,vv_id)
      if (err .NE. NF_NOERR) go to 10
      call set_attributes(ncid,vv_id,long_name='meridional vel.',units='m/s', &
                          FillValue=fv,missing_value=mv,valid_range=vr)

!     vertical velocity
      err = nf_def_var(ncid,'w',NF_REAL,4,f4_dims,w_id)
      if (err .NE. NF_NOERR) go to 10
      call set_attributes(ncid,w_id,long_name='vertical vel.',units='m/s', &
                          FillValue=fv,missing_value=mv,valid_range=vr)
   end if

   if (save_strho) then

      if (save_s) then
         fv = salt_missing
         mv = salt_missing
         vr(1) =  0.
         vr(2) = 40.
         err = nf_def_var(ncid,'salt',NF_REAL,4,f4_dims,salt_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,salt_id,long_name='salinity',units='PSU', &
                             FillValue=fv,missing_value=mv,valid_range=vr)
      end if

      if (save_t) then
         fv = temp_missing
         mv = temp_missing
         vr(1) =  0.
         vr(2) = 40.
         err = nf_def_var(ncid,'temp',NF_REAL,4,f4_dims,temp_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,temp_id,long_name='temperature',units='degC',&
                             FillValue=fv,missing_value=mv,valid_range=vr)
      end if

      if (save_rho) then
         fv = rho_missing
         mv = rho_missing
         vr(1) =  0.
         vr(2) = 30.
         err = nf_def_var(ncid,'sigma_t',NF_REAL,4,f4_dims,sigma_t_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,sigma_t_id,long_name='sigma_t',units='kg/m3',&
                             FillValue=fv,missing_value=mv,valid_range=vr)
      end if

      if (save_rad) then
         fv = rad_missing
         mv = rad_missing
         vr(1) =  0.
         vr(2) = 1354.
         err = nf_def_var(ncid,'radiation',NF_REAL,4,f4_dims,rad_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,rad_id,long_name='radiation',units='W/m2',&
                             FillValue=fv,missing_value=mv,valid_range=vr)
      end if

   end if

   if (save_turb) then

      if (save_tke) then
         fv = tke_missing
         mv = tke_missing
         vr(1) = 0.
         vr(2) = 0.2
         err = nf_def_var(ncid,'tke',NF_REAL,4,f4_dims,tke_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,tke_id,long_name='TKE',units='m2/s2', &
                             FillValue=fv,missing_value=mv,valid_range=vr)
      end if

      if (save_num) then
         fv = num_missing
         mv = num_missing
         vr(1) = 0.
         vr(2) = 0.2
         err = nf_def_var(ncid,'num',NF_REAL,4,f4_dims,num_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,num_id,long_name='viscosity',units='m2/s', &
                             FillValue=fv,missing_value=mv,valid_range=vr)
      end if

      if (save_nuh) then
         fv = nuh_missing
         mv = nuh_missing
         vr(1) = 0.
         vr(2) = 0.2
         err = nf_def_var(ncid,'nuh',NF_REAL,4,f4_dims,nuh_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,nuh_id,long_name='diffusivity',units='m2/s', &
                             FillValue=fv,missing_value=mv,valid_range=vr)
      end if

      if (save_eps) then
         fv = eps_missing
         mv = eps_missing
         vr(1) = 0.
         vr(2) = 0.2
         err = nf_def_var(ncid,'diss',NF_REAL,4,f4_dims,eps_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,eps_id,long_name='dissipation',units='m2/s3',&
                             FillValue=fv,missing_value=mv,valid_range=vr)
      end if
   end if

   if (save_SS_NN) then

      fv = SS_missing
      mv = SS_missing
      vr(1) = 0.
      vr(2) = 0.01
      err = nf_def_var(ncid,'SS',NF_REAL,4,f4_dims,SS_id)
      if (err .NE. NF_NOERR) go to 10
      call set_attributes(ncid,SS_id,long_name='shear stress',units='s-1',&
                          FillValue=fv,missing_value=mv,valid_range=vr)
#ifndef NO_BAROCLINIC
      fv = NN_missing
      mv = NN_missing
      vr(1) = -0.001
      vr(2) = 0.01
      err = nf_def_var(ncid,'NN',NF_REAL,4,f4_dims,NN_id)
      if (err .NE. NF_NOERR) go to 10
      call set_attributes(ncid,NN_id,long_name='Brunt-Vaisala frequency', &
                          units='s-1',&
                          FillValue=fv,missing_value=mv,valid_range=vr)
#endif

   end if
#ifdef SPM
   if (spm_save) then
      fv = spm_missing
      mv = spm_missing
      err = nf_def_var(ncid,'spm_pool',NF_REAL,3,f3_dims,spmpool_id) 
      if (err .NE. NF_NOERR) go to 10
      vr(1) = 0.
      vr(2) = 10.
      call set_attributes(ncid,spmpool_id,long_name='bottom spm pool', &
                          units='kg/m2', & 
                          FillValue=fv,missing_value=mv,valid_range=vr)
      vr(1) =  0.
      vr(2) = 30.
      err = nf_def_var(ncid,'spm',NF_REAL,4,f4_dims,spm_id)
      if (err .NE. NF_NOERR) go to 10
      call set_attributes(ncid,spm_id,  &
                          long_name='suspended particulate matter', &
                          units='kg/m3', &
                          FillValue=fv,missing_value=mv,valid_range=vr)
   end if
#endif

#ifdef GETM_BIO
   allocate(bio_ids(numc),stat=rc)
   if (rc /= 0) stop 'init_3d_ncdf(): Error allocating memory (bio_ids)'
   STDERR numc
   fv = bio_missing
   mv = bio_missing
   vr(1) = _ZERO_
   vr(2) = 9999.
   do n=1,numc
      err = nf_def_var(ncid,var_names(n),NF_REAL,4,f4_dims,bio_ids(n))
      if (err .NE.  NF_NOERR) go to 10
      call set_attributes(ncid,bio_ids(n), &
                          long_name=trim(var_long(n)), &
                          units=trim(var_units(n)), &
                          FillValue=fv,missing_value=mv,valid_range=vr)
   end do
#endif

!  globals
   err = nf_put_att_text(ncid,NF_GLOBAL,'title',LEN_TRIM(title),title)
   if (err .NE. NF_NOERR) go to 10

   err = nf_put_att_text(ncid,NF_GLOBAL,'history',LEN_TRIM(history),history)
   if (err .NE. NF_NOERR) go to 10

   ! leave define mode
   err = nf_enddef(ncid)
   if (err .NE. NF_NOERR) go to 10

   return

   10 FATAL 'init_3d_ncdf: ',nf_strerror(err)
   stop 'init_3d_ncdf'
   end subroutine init_3d_ncdf
!EOC

!-----------------------------------------------------------------------
! Copyright (C) 2001 - Hans Burchard and Karsten Bolding (BBH)         !
!-----------------------------------------------------------------------
